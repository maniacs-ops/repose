buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
        classpath 'org.apache.httpcomponents:httpclient:4.4.1'
        classpath 'org.apache.httpcomponents:fluent-hc:4.4.1'
    }
}


import com.bmuschko.gradle.vagrant.tasks.VagrantDestroy
import com.bmuschko.gradle.vagrant.tasks.VagrantUp
import com.bmuschko.gradle.vagrant.tasks.VagrantSsh
import org.apache.http.client.fluent.Request

apply plugin: 'com.bmuschko.vagrant-base'

File debBoxDir = file("$buildDir/deb")
File rpmBoxDir = file("$buildDir/rpm")

vagrant {
    environmentVariables {
        variable 'REPOSE_VERSION', project.hasProperty('release-version') ? property('release-version') : 'default'
    }
}

task copyDebFiles() {
    inputs.files(file("$projectDir/src/vagrant/deb"),
                 file("$projectDir/src/vagrant/scripts"),
                 file("$projectDir/src/vagrant/fake-services"),
                 file("$projectDir/src/config"))
    outputs.dir debBoxDir

    doLast {
        //make the deb directory
        debBoxDir.mkdirs()

        //copy the vagrant file
        copy {
            from "$projectDir/src/vagrant/deb"
            into debBoxDir
        }

        //copy the scripts
        copy {
            from "$projectDir/src/vagrant/scripts"
            into "$debBoxDir/scripts"
        }

        //copy the fake services
        copy {
            from "$projectDir/src/vagrant/fake-services"
            into "$debBoxDir/fake-services"
        }

        //copy the configs
        copy {
            from "$projectDir/src/config"
            into "$debBoxDir/etc_repose"
        }
    }
}

task vagrantUpDeb(type: VagrantUp) {
    dependsOn 'copyDebFiles'
    finalizedBy 'vagrantDestroyDeb'

    boxDir = debBoxDir
}

task vagrantSshSmokeTestDeb(type: VagrantSsh) {
    dependsOn 'vagrantUpDeb'
    finalizedBy 'vagrantDestroyDeb'

    boxDir = debBoxDir
}

task vagrantDestroyDeb(type: VagrantDestroy) {
    boxDir = debBoxDir
}

task copyRpmFiles() {
    inputs.files(file("$projectDir/src/vagrant/rpm"),
            file("$projectDir/src/vagrant/scripts"),
            file("$projectDir/src/vagrant/fake-services"),
            file("$projectDir/src/config"))
    outputs.dir rpmBoxDir

    doLast {
        //make the rpm directory
        rpmBoxDir.mkdirs()

        //copy the vagrant file
        copy {
            from "$projectDir/src/vagrant/rpm"
            into rpmBoxDir
        }

        //copy the scripts
        copy {
            from "$projectDir/src/vagrant/scripts"
            into "$rpmBoxDir/scripts"
        }

        //copy the fake services
        copy {
            from "$projectDir/src/vagrant/fake-services"
            into "$rpmBoxDir/fake-services"
        }

        //copy the configs
        copy {
            from "$projectDir/src/config"
            into "$rpmBoxDir/etc_repose"
        }
    }
}

task vagrantUpRpm(type: VagrantUp) {
    dependsOn 'copyRpmFiles'
    finalizedBy 'vagrantDestroyRpm'

    boxDir = rpmBoxDir
}

task vagrantSshSmokeTestRpm(type: VagrantSsh) {
    dependsOn 'vagrantUpRpm'
    finalizedBy 'vagrantDestroyRpm'

    boxDir = debBoxDir
}

task vagrantDestroyRpm(type: VagrantDestroy) {
    boxDir = rpmBoxDir
}

task('smokeTestDeb', description: 'tests if the DEB Repose instance can handle basic requests', dependsOn: 'vagrantUpDeb') << {
    // TODO: Replace retry logic. How do we know when Repose is ready?
    // TODO: Don't repeat this logic in multiple tasks -- make this task a type?
    def responseCode = -1
    def attempt = 0
    while (responseCode != 200 && attempt < 10) {
      attempt++
      responseCode = Request.get("http://localhost:8080")
        .execute()
        .returnResponse()
        .getStatusLine()
        .getStatusCode()
    }

    if (responseCode != 200) {
      throw new GradleException('HTTP requests to the Repose instance failed')
    }
}

task('smokeTestRpm', description: 'tests if the RPM Repose instance can handle basic requests', dependsOn: 'vagrantUpRpm') << {
    def responseCode = -1
    def attempt = 0
    while (responseCode != 200 && attempt < 10) {
      attempt++
      responseCode = Request.get("http://localhost:8080")
        .execute()
        .returnResponse()
        .getStatusLine()
        .getStatusCode()
    }

    if (responseCode != 200) {
      throw new GradleException('HTTP requests to the Repose instance failed')
    }
}

task('smokeTest', description: 'tests if the Repose instance can handle basic requests', group: 'release', dependsOn: ['smokeTestDeb', 'smokeTestRpm'])

// Note: At this time, release verification will be handled out-of-band with the release. Therefore,
//       no wiring was added to place the 'smokeTest' task into existing task flows.
