buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
        classpath 'org.apache.httpcomponents:httpclient:4.4.1'
        classpath 'org.apache.httpcomponents:fluent-hc:4.4.1'
    }
}


import com.bmuschko.gradle.vagrant.tasks.VagrantDestroy
import com.bmuschko.gradle.vagrant.tasks.VagrantUp
import org.apache.http.client.fluent.Request

apply plugin: 'com.bmuschko.vagrant-base'

// TODO: Don't hardcode this -- since it's just a flat directory, we can't add a dependency... move those files to
//       this project?
String baseBoxDir = project.projectDir.absolutePath + '/../../artifacts/src/vagrant/'
File debBoxDir = file(baseBoxDir + 'deb')
File rpmBoxDir = file(baseBoxDir + 'rpm')

vagrant {
    environmentVariables {
        // TODO: Should this be defaulted, or should we explicitly check for
        //       the property when the task is run?
        variable 'REPOSE_VERSION', hasProperty('release-version') ? property('release-version') : 'local'
    }
}

task vagrantUpDeb(type: VagrantUp) {
    finalizedBy 'vagrantDestroyDeb'
    boxDir = debBoxDir
}

task vagrantDestroyDeb(type: VagrantDestroy) {
    boxDir = debBoxDir
}

task vagrantUpRpm(type: VagrantUp) {
    finalizedBy 'vagrantDestroyRpm'
    boxDir = rpmBoxDir
}

task vagrantDestroyRpm(type: VagrantDestroy) {
    boxDir = rpmBoxDir
}

task('smokeTestDeb', description: 'tests if the DEB Repose instance can handle basic requests', dependsOn: 'vagrantUpDeb') << {
    // TODO: Replace retry logic. How do we know when Repose is ready?
    // TODO: Don't repeat this logic in multiple tasks -- make this task a type?
    def responseCode = -1
    def attempt = 0
    while (responseCode != 200 && attempt < 10) {
      attempt++
      responseCode = Request.get("http://localhost:8080")
        .execute()
        .returnResponse()
        .getStatusLine()
        .getStatusCode()
    }

    if (responseCode != 200) {
      throw new GradleException('HTTP requests to the Repose instance failed')
    }
}

task('smokeTestRpm', description: 'tests if the RPM Repose instance can handle basic requests', dependsOn: 'vagrantUpRpm') << {
    def responseCode = -1
    def attempt = 0
    while (responseCode != 200 && attempt < 10) {
      attempt++
      responseCode = Request.get("http://localhost:8080")
        .execute()
        .returnResponse()
        .getStatusLine()
        .getStatusCode()
    }

    if (responseCode != 200) {
      throw new GradleException('HTTP requests to the Repose instance failed')
    }
}

task('smokeTest', description: 'tests if the Repose instance can handle basic requests', group: 'release', dependsOn: ['smokeTestDeb', 'smokeTestRpm'])

// Note: At this time, release verification will be handled out-of-band with the release. Therefore,
//       no wiring was added to place the 'smokeTest' task into existing task flows.
